// ZKVN Engine
// Zandronum 2.0+ Edition
// Decorate

// Constants
#include "decorate/zkvn_const.txt"
const int VNC_MRNOBODY = 1;

// Mr Nobody
ACTOR MrNobody : SwitchingDecoration 1337
{
	//$Category ZK-VN Demo
	//$Title "Mr. Nobody"
	//$Sprite TROOA1
	Radius 20
	Height 56
	Activation THINGSPEC_Switch|THINGSPEC_ThingTargets
	+USESPECIAL
	+SOLID
	States
	{
	Spawn:
		TROO A 1
		Loop
	Active:
		// Prevent multiple dialogue activations
		TNT1 A 0 A_ChangeFlag("USESPECIAL", false)
		
		//
		// Make sure that you use ACS_ExecuteWithResult for all scripts or you will run into issues.
		//
		// Syntax:
		// SCR_VN_SET_HOLD_TIME, <Hold Time>
		// SCR_VN_ADD_DIALOGUE, <Character ID>, <Graphic ID>, <Dialogue ID>
		// SCR_VN_ADD_CHOICE, <Language Choice ID>, <Custom Choice ID>
		// SCR_VN_ADD_CHOICE_START, <Type>, <Numbered> // Type 0 = Default, Type 1 = Alternate, Type 2 = Default with UI, Type 3 = Alternate with UI
		// SCR_VN_ADD_CHOICE_END // This script is a marker, so it takes no arguments
		// SCR_VN_CHOICE_MADE // This script will return the custom choice ID of the last choice the player made
		// SCR_VN_CHOICE_DONE // This script should be used inside A_JumpIf and followed by a "Wait"
		// SCR_VN_ISQUEUEEMPTY // This script should be used inside A_JumpIf and followed by a "Wait"
		// SCR_VN_FORCE_FADEIN // Forces the next piece of dialogue to fade in
		// SCR_VN_SET_GRAPHIC_SIZE, <Width>, <Height>
		// SCR_VN_VOICE_ACTING_ON // Turns on voice acting
		// SCR_VN_VOICE_ACTING_OFF // Turns off voice acting
		//
		// The "custom choice ID" is a number that you associate with the choice,
		// which is unique to the choice. SCR_VN_CHOICE_MADE uses this number rather
		// than the language choice ID because this way it allows more control over conversations.
		//
		// If the Graphic ID is -1, then no graphic will be displayed.
		// This is useful if you want to have dialogue with no portrait.
		//
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 ACS_ExecuteWithResult(SCR_VN_SET_GRAPHIC_SIZE, 1280, 960) // Mr. Nobody's graphic is 1280x960 pixels
		TNT1 A 0 ACS_ExecuteWithResult(SCR_VN_SET_HOLD_TIME, -1)
		TNT1 A 0 ACS_ExecuteWithResult(SCR_VN_ADD_DIALOGUE, VNC_MRNOBODY, 1, 1)
		TNT1 A 0 ACS_ExecuteWithResult(SCR_VN_ADD_CHOICE_START) // Using AddChoiceStart with no parameters will make it use type 0 without numbering
		TNT1 A 0 ACS_ExecuteWithResult(SCR_VN_ADD_CHOICE, 1, 1)
		TNT1 A 0 ACS_ExecuteWithResult(SCR_VN_ADD_CHOICE, 2, 2)
		TNT1 A 0 ACS_ExecuteWithResult(SCR_VN_ADD_CHOICE_END)
		TROO A 1 A_JumpIf(ACS_ExecuteWithResult(SCR_VN_CHOICE_DONE), 1)
		Wait
		TNT1 A 0 A_JumpIf(1 == ACS_ExecuteWithResult(SCR_VN_CHOICE_MADE), "Choice1")
		TNT1 A 0 A_JumpIf(2 == ACS_ExecuteWithResult(SCR_VN_CHOICE_MADE), "Choice2")
		Goto Done
		
	Choice1:
		TNT1 A 0 ACS_ExecuteWithResult(SCR_VN_ADD_DIALOGUE, VNC_MRNOBODY, 1, 2)
		Goto Done
	Choice2:
		TNT1 A 0 ACS_ExecuteWithResult(SCR_VN_ADD_DIALOGUE, VNC_MRNOBODY, 1, 3)
		Goto Done
		
	Done:
		// Here we wait for the stack to empty (all dialogue to be cleared)
		TROO A 1 A_JumpIf(ACS_ExecuteWithResult(SCR_VN_ISQUEUEEMPTY), 1)
		Wait
		TNT1 A 0 A_ChangeFlag("USESPECIAL", true)
		Goto Spawn
	}
}